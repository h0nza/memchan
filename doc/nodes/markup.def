# -*- tcl -*-
# commands for logical markup

proc strong {text} {return [cgi_bold $text]}
proc cem    {text} {return [font color=red $text]}

proc lang   {text} {return [cgi_typewriter $text]}
proc afile  {text} {return [cgi_typewriter $text]}
proc arg    {text} {return [cgi_typewriter "[cgi_lt]$text[cgi_gt]"]}
proc cmd    {text} {return [cgi_typewriter $text]}
proc lib    {text} {return [cgi_typewriter $text]}
proc opt    {text} {return [cgi_typewriter $text]}
proc var    {text} {return [cgi_typewriter $text]}
proc man    {text} {return [cgi_typewriter $text]}
proc os     {text} {return [cgi_typewriter $text]}


# special commands to make the definition of links to people easier
# 3 types:
# * A person with an email address
# * A person with a home page
# * A person having both an email address and a home page

proc personH {code name home} {
    link $code $name $home
    proc $code {} "return \[link $code\]"
    return
}

proc personM {code name email} {
    link $code $name "mailto:$email"
    proc $code {} "return \[link $code\]"
    return
}

proc personMH {code name email home} {
    # ignore the email address for now, assume that
    # it is placed on the homepage somewhere
    personH $code $name $home
}


proc theading {args} {
    set script  [::pool::list::pop args]
    set text    [::pool::list::pop args]
    set options $args

    eval table bgcolor=black border=0 cellspacing=0 cellpadding=2 $options {{
	table_row {
	    table_data {
		cgi_puts [font color=white [strong $text]]
	    }
	}
	table_row {
	    table_data {
		uplevel $script
	    }
	}
    }}
}


proc kvrow {string desc} {
    table_row valign=top {
	td [font color=white [strong $string]]
	table_data bgcolor=#ffffcc {
	    uplevel $desc
	}
    }
}

proc kvtable {title script} {
    theading width=100% $title {
	table border=1 width=100% cellspacing=0 cellpadding=2 bgcolor=steelblue {
	    table_row valign=top {
		td [font color=white [strong Key]]
		td [font color=white [strong Value]]
	    }

	    uplevel $script
	}
    }
}


proc cmdtable {title script} {
    theading width=100% $title {
	table border=1 width=100% cellspacing=0 cellpadding=2 {
	    table_row valign=top {
		td [font color=white [strong Subcmd]]
		td [font color=white [strong Description]]
	    }

	    uplevel $script
	}
    }
}

proc cmdrow {cmd argdef script {example {}}} {
    table_row valign=top bgcolor=steelblue {
	td [font color=white [strong $cmd]]
	table_data {
	    syntax $argdef $script

	    if {$example != {}} {
		example $example
	    }
	}
    }
}


proc syntax {argdef script} {
    theading width=100% bgcolor=steelblue $argdef {
	table border=0 width=100% cellspacing=0 cellpadding=2 bgcolor=#ffffcc {
	    table_row {
		table_data {
		    uplevel $script
		}
	    }
	}
    }
}


proc example {text} {
    theading width=100% Example {
	table border=1 width=100% cellspacing=0 cellpadding=2 bgcolor=#ffffcc {
	    table_row {
		table_data {
		    cgi_preformatted {
			cgi_puts $text
		    }
		}
	    }
	}
    }
}
